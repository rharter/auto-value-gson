import org.gradle.internal.jvm.Jvm

plugins {
    id 'com.github.johnrengelman.shadow' version '7.0.0'
    id 'java-library'
    id 'maven-publish'
}

//<editor-fold desc="ShadowJar configuration">
// disable default jar tasks
configurations.runtimeOnly.artifacts.removeAll { it.archiveTask.is jar }
tasks.getByName('jar').enabled = false
// create extra configuration for shaded dependencies, so they're not included in the pom
def shadedConfig = configurations.create('compileShaded')
configurations.compileOnly.extendsFrom(shadedConfig)
shadowJar {
    minimize()
    classifier = ''
    configurations = [shadedConfig]
    relocate 'com.google.auto.common', 'autovaluegson.factory.shaded.com.google.auto.common'
    relocate 'com.google.common', 'autovaluegson.factory.shaded.com.google.common'
    relocate 'com.google.thirdparty', 'autovaluegson.factory.shaded.com.google.thirdparty'
    exclude 'afu/**'
    exclude 'org/**'
    exclude 'com/google/errorprone/annotations/**'
    exclude 'com/google/j2objc/annotations/**'
    exclude 'javax/**'
    exclude 'META-INF/maven/com.google.auto/auto-common/**'
}
artifacts {
    runtimeOnly shadowJar
    archives shadowJar
}
//</editor-fold>

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

dependencies {
    annotationProcessor "net.ltgt.gradle.incap:incap-processor:0.3"
    annotationProcessor 'com.google.auto.service:auto-service:1.0'
    compileOnly "net.ltgt.gradle.incap:incap:0.3"
    compileOnly 'com.google.auto.service:auto-service:1.0'

    api 'com.google.auto.value:auto-value-annotations:1.8.2'
    api project(':auto-value-gson-extension')
    compileShaded 'com.google.auto:auto-common:1.1.2'
    compileShaded 'com.google.guava:guava:30.1.1-jre'
    implementation 'com.squareup:javapoet:1.13.0'
    implementation deps.gson

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'com.google.truth:truth:1.1.3'
    testImplementation 'com.google.testing.compile:compile-testing:0.19'
    testCompileOnly "net.ltgt.gradle.incap:incap:0.3"
    testImplementation files(Jvm.current().getToolsJar())
}

apply from: rootProject.file('gradle/gradle-mvn-push.gradle')
